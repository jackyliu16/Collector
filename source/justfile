IN_NIX_SHELL := env("IN_NIX_SHELL", "false")
SOURCE_DIR := source_directory()

_default:
    @just --list

# PRIVATE COMMAND
# --------------------------------------------------

# Run Command in devShell
_source-run cmd *args:
    #!/usr/bin/env -S sh -eu
    if [ "{{ IN_NIX_SHELL }}" = "false" ]; then
        cd {{ SOURCE_DIR }} && nix develop "{{ SOURCE_DIR }}"# --command {{ cmd }} {{ args }}
    else
        {{ cmd }} {{ args }}
    fi

# PUBLIC COMMAND
# --------------------------------------------------

# [S] Auto-format the source tree
fmt:
   just _source-run treefmt

# [S] Run 'cargo run' on the project
run *ARGS:
    just _source-run cargo run {{ARGS}}

# [S] Run 'cargo build' on the project
build *ARGS:
    just _source-run cargo build {{ARGS}}
# [S] Run 'cargo build' on the project and build win apps
build-win *ARGS:
    just _source-run cargo build --target x86_64-pc-windows-gnu --manifest-path {{ SOURCE_DIR }}/Cargo.toml {{ ARGS }}
    # TODO: --manifest-path ways seems not working and will case tuns of error output
# [S] Run 'cargo build' on the project and release win apps 
release-win:
    just build-win --release 
# [S] Run 'cargo watch' to run the project (auto-recompiles)
watch *ARGS:
    just _source-run cargo watch -x run -- {{ARGS}}

# [S] Open source code via Zellij layout
edit:
	just _source-run zellij -l {{ SOURCE_DIR }}/.zellij.kdl

# [S] Clean Files generate by cargo 
clean:
    just _source-run cargo clean
