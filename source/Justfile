IN_NIX_SHELL := env("IN_NIX_SHELL", "false")
SOURCE_DIR := source_directory()

_default:
    @just --list

# PRIVATE COMMAND
# -------------------------------------------------- 

# Run Command in devShell
_source-run cmd *args:
    #!/usr/bin/env -S sh -eu
    if [ "{{ IN_NIX_SHELL }}" = "false" ]; then
        cd {{ SOURCE_DIR }} && nix develop "{{ SOURCE_DIR }}"# --command {{ cmd }} {{ args }}
    else
        {{ cmd }} {{ args }}
    fi

# PUBLIC COMMAND
# -------------------------------------------------- 

# Auto-format the source tree
src-fmt:
   just _source-run treefmt

# Run 'cargo run' on the project
src-run *ARGS: 
    just _source-run cargo run {{ARGS}}

# Run 'cargo build' on the project
src-build *ARGS: 
    just _source-run cargo build {{ARGS}}

# Run 'cargo watch' to run the project (auto-recompiles)
src-watch *ARGS:
    just _source-run cargo watch -x run -- {{ARGS}}

# Open default Zellij layout
src-edit: 
	just _source-run zellij -l {{ SOURCE_DIR }}/.zellij.kdl

